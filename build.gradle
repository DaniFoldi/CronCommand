plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'hu.nugget'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    compileOnly ('com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'jsr305'
    }

    compileOnly 'org.jetbrains:annotations:20.1.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.dagger:dagger:2.33'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.33'

    implementation ('com.electronwill.night-config:yaml:3.6.0') {
        exclude module: 'snakeyaml'
    }
}

processResources {
    from('src/main/resources') {
        expand(version: project.version)
    }
}

shadowJar {
    minimize {
        exclude(dependency('com.electronwill.night-config:.*:.*'))
    }

    relocate 'com.electronwill.nightconfig', 'hu.nugget.croncommand.reloc.config'
    relocate 'javax.inject', 'hu.nugget.croncommand.reloc.inject'
    relocate 'dagger', 'hu.nugget.croncommand.reloc.dagger'

    getArchiveFileName().set("${project.name}-${project.version}.jar")
}